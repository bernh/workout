use wtree::{Run, Workout, DistanceAndTime, pace2speed};
use config::get_pace;

grammar;

pub Workout_main : Workout = {
    <p:Parts>  => { 
        let mut w = Workout::new(1);
        w.nodes = p;
        w
    },
};

pub Parts : Vec<Box<DistanceAndTime>> = {
    <p:Part> => vec![p],
    <l:Parts> "+" <r:Part> => {
        let mut l = l;
        l.push(r);
        l
    }
};

pub Part : Box<DistanceAndTime> = {
    <s:Step> => Box::new(s),
    <w:Workout> => Box::new(w),
};

pub Workout : Workout = {
    <n:Repetition> "*" "(" <p:Parts> ")" => {
        let mut w = Workout::new(n);
        w.nodes = p;
        w
    }
};

pub Step : Run = {
    TimeRun,
    DistanceRun,
};

TimeRun : Run = {
    <t:Time> "min" <e:Effort> => Run::from_time(t as f32 * 60.0, pace2speed(get_pace(&e))),
};

DistanceRun : Run = {
    <d:Distance><e:Effort> => {
        if d < 100.0 {
            Run::from_distance(d * 1000.0 , pace2speed(get_pace(&e)))
        } else {
            Run::from_distance(d, pace2speed(get_pace(&e)))
        }

    }
};

Time: i32 = {
    r"[0-9]+" => <>.parse::<i32>().unwrap(),
};

Distance: f32 = {
    r"[0-9]+"         => <>.parse::<f32>().unwrap(),
    r"[0-9]+\.[0-9]+" => <>.parse::<f32>().unwrap(),
};

Repetition: i32 = {
    r"[0-9]+" => <>.parse::<i32>().unwrap(),
};


Effort : String = {
    r"[ELMTHIR]" => <>.to_string(),
    "jog"        => <>.to_string(),
    "jg"         => <>.to_string(),
    "rest"       => <>.to_string(),
    "rst"        => <>.to_string(),
};
